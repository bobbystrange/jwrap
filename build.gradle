plugins {
    // https://plugins.gradle.org/plugin/org.springframework.boot
    id 'org.springframework.boot' version '2.5.0' apply false
    // https://plugins.gradle.org/plugin/io.spring.dependency-management
    id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
}

subprojects {
    apply plugin: 'java-library'

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'
    sourceCompatibility = '8'
    version = VERSION
    group = GROUP_ID

    // set up the default repositories to mavenCentral & maven snapshots
    if (project.repositories.isEmpty()) {
        repositories {
            mavenCentral()
            maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        }
    }

    ext {
        slf4jVersion = "1.7.30"
        lombokVersion = "1.18.12"
        junitVersion = "4.12"
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        testCompileOnly "org.projectlombok:lombok:$lombokVersion"
        testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
        implementation "org.slf4j:slf4j-api:$slf4jVersion"
        testImplementation "junit:junit:$junitVersion"

        implementation "org.dreamcat:common-core:$COMMON_VERSION"
    }

    if (projectDir.getParent() == rootDir.path) {
        // apply from: "../publish.gradle"
        apply from: "../publish-mavenCentral.gradle"
    }
}
