plugins {
    // https://plugins.gradle.org/plugin/org.springframework.boot
    id 'org.springframework.boot' version '2.4.2' apply false
    // https://plugins.gradle.org/plugin/io.spring.dependency-management
    id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
    // https://plugins.gradle.org/plugin/org.sonarqube
    id "org.sonarqube" version "3.1.1" apply false
}

subprojects {
    apply plugin: 'java-library'

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'
    sourceCompatibility = '8'
    version = VERSION
    group = GROUP_ID

    ext {
        slf4jVersion = "1.7.30"
        lombokVersion = "1.18.12"
        junitVersion = "4.12"
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        testCompileOnly "org.projectlombok:lombok:$lombokVersion"
        testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
        implementation "org.slf4j:slf4j-api:$slf4jVersion"
        testImplementation "junit:junit:$junitVersion"

        implementation "org.dreamcat:common-core:$COMMON_VERSION"
    }

    if (projectDir.getParent() == rootDir.path) {
        // apply from: "../publish.gradle"
        apply from: "../publish-mavenCentral.gradle"
        apply plugin: 'org.sonarqube'

        sonarqube {
            // skipProject = true
            properties {
                property "sonar.projectKey", "${project.name}"
                property "sonar.organization", "tuke"
                property "sonar.host.url", "https://sonarcloud.io"
            }
        }
    }
}
