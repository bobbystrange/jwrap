apply plugin: 'maven-publish'

def releasesRepoUrl = "http://nexus.singlar.org/repository/maven-releases/"
def snapshotsRepoUrl = "http://nexus.singlar.org/repository/maven-snapshots/"
def isReleaseVersion = !version.endsWith("SNAPSHOT")

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks.withType(Javadoc) {
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar) {
    archiveClassifier.set 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier.set 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            groupId project.group
            artifactId project.name
            version project.version
            pom {
                name = project.name
                description = project.description
            }
        }
    }
    repositories {
        maven {
            url isReleaseVersion ? releasesRepoUrl : snapshotsRepoUrl
            credentials {
                username project.findProperty("nexus.username")  as String
                password project.findProperty("nexus.password") as String
            }
        }
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
